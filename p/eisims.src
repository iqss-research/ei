/*
**  This archive is part of the program EI
**  (C) Copyright 1995-2001 Gary King
**  All Rights Reserved.
*/
/*
   {betaBs,betaWs} = psim1(T,X,N,Zb,Zw,MLpsi,MLvc);
**
** precinct-level parameter simulations
**
** INPUTS:
** T    = dependent variable (p x 1)
** X    = explanatory variable (p x 1)
** N    = denominator of T and X (e.g., total voting age population) (p x 1)
** Zb   = 1 or explanatory vars to predict beta-b 
** Zw   = 1 or explanatory vars to predict beta-w 
** MLpsi,MLvc = output from quadcml()
**
** GLOBAL:
** _EbetaWs = 1 compute betaws as output
**            0 set to missing (default) (can use eiread to compute)
**            
** OUTPUT:
** betaBs,betaWs  =  (p x sims) matrices of simulations
**
*/
#include ei.ext;
proc 2=psim1(T,X,tvap,Zb,Zw,MLpsi,MLvc);
  local PSIsims,k,betaBs,betaWs,Bbeta,aggs,Ebb,Vbb,tt,dataset,
  etaB,etaW,etaBs,etaWs,c,c0,c1,i,rs,r,temp,temp1,res;
  
  /* ESTIMATION VARIABILITY */
  if _Eprt>=2;
    "Simulating estimation variation...";
  endif;
  /* _Eselect is applied within packdta, so dataset is _Eselect'd but
      x,Zb,Zw,t, which are used for fundamental variability, are not */
  dataset=packdta(x,Zb,Zw,t);
  
  /* importance sampling for b's and normal for eta's:  sims x rows(MLpsi) */
  etaB=MLpsi[rows(MLpsi)-1];
  etaW=MLpsi[rows(MLpsi)];
  if rows(_Eeta)==1;
    etaBs=0;
    etaWs=0;
  elseif rows(_Eeta)==4;
    etaBs=_Eeta[3];
    etaWs=_Eeta[4];
  elseif _Eeta[1]==4;
    etaBs=0;
    etaWs=_Eeta[3];
  elseif _Eeta[1]==5;
    etaBs=_Eeta[3];
    etaWs=0;
  endif;
  if _Erho[1]==0;
    _EisFix=MLpsi[(rows(MLpsi)-2):rows(MLpsi)];
    temp=rndisamp(&eiloglik,trimr(MLpsi,0,3),MLvc[1:rows(MLpsi)-3,
         1:rows(MLpsi)-3],dataset,_Esims);
  else;
    _EisFix=MLpsi[(rows(MLpsi)-1):rows(MLpsi)];
    temp=rndisamp(&eiloglik,trimr(MLpsi,0,2),MLvc[1:rows(MLpsi)-2,
         1:rows(MLpsi)-2],dataset,_Esims);
  endif;

  if ismiss(temp); @ if _ei_vc={-1 0} fails try _ei_vc={4 0} @ 
    temp=_ei_vc;
    temp1=_Ghactual;
    _ei_vc=temp[(temp1+1):rows(_ei_vc),.];
    MLvc=gvc(&eiloglik,MLpsi,dataset);
    if _Erho[1]==0;
      MLvc=MLvc~zeros(rows(MLvc),1);
      MLvc=MLvc|zeros(1,cols(MLvc));
      MLvc[rows(MLvc),cols(MLvc)]=0.00000000001;
    endif;
    if scalmiss(MLvc);
      "quadml: couldn't compute positive definite variance matrix";
      MLvc=miss(1,1);
    endif;
    _Eres=vput(_Eres,MLvc,"vcphi");  
    {betaBs, betaWs}=psim1(T,X,tvap,Zb,Zw,MLpsi,MLvc);
    _ei_vc=temp;
    _ghactual=temp1+1;
    _Eres=vput(_Eres,_ei_vc,"_EI_vc");
    _Eres=vput(_Eres,_ghactual,"GhActual");
    retp(betaBs, betaWs);
  endif;

  if _Erho[1]==0;  
    PSIsims=temp~rndmn(_Erho[2],0,_Esims)~rndmn(etaB,etaBs,_Esims)~
            rndmn(etaW,etaBs,_Esims);
  else;
    PSIsims=temp~rndmn(etaB,etaBs,_Esims)~rndmn(etaW,etaBs,_Esims);
  endif;
	  
  if _EisChk;                          
    _Eres=vput(_Eres,psisims,"PhiSims");  @ save name corresponds to book @ 
  else;
    _Eres=vput(_Eres,meanc(psisims)~stdc(psisims),"PhiSims");
  endif;
  
  /* FUNDAMENTAL VARIABILITY */
  /* Applies to all observations */
  if _Eprt>=2;
    "Simulating fundamental variability...";
  endif;
  rs=rows(x);
  betaBs=zeros(rs,_Esims);
  betaWs=betaBs;
  {Bbeta,aggs}=bounds1(t,x,tvap);
  for k (1, _Esims, 1);
    {tt,tt,tt,tt,Ebb,Vbb} = exvar(T,x,eirepar(PSIsims[k+0,.]',Zb,Zw,x));
    betaBs[.,k+0] = rndtni(Ebb,Vbb,Bbeta[.,1 2]);
  endfor;

  /* compute betaWs from betaBs deterministically */
  if _Ebetaws;
    betaWs=betab2w(t,x,betaBs);
  else;
    betaWs=miss(1,1);
  endif;
  
  retp(betaBs,betaWs);
endp;

/*
** betaw = betab2w(t,x,betab);
**
** compute betaw deterministically given betab
**
** INPUT DIMENSIONS:
** rows of all inputs must be p
** cols(t) must equal 1
** cols(x) must be 1 (for ei) or _Esims (for ei2)
** cols(betab) must be 1 (for mean posterior) or _Esims (for betaBs)
*/
proc betab2w(t,x,betab);
  local c,c0,c1,betaw,i,col,x1;
  col=cols(x);
  x1=1-x;
  betaW=betaB;
  
  if col==1;
    {c,c0,c1} = homoindx(x);
    if not(scalmiss(c));
      betaW[c,.] = (t[c]./x1[c])-((betaB[c,.].*x[c])./x1[c]);
    endif;
    if not(scalmiss(c0));
      betaW[c0,.] = t[c0].*ones(rows(c0),cols(betaB));
    endif;
    if not(scalmiss(c1));
      betaW[c1,.] = miss(1,1).*ones(rows(c1),cols(betaB));
    endif;
    
  else;
    if col/=cols(betaB);
      "EI internal error, betab2w";
      stop;
    endif;
    for i (1, col, 1);
      {c,c0,c1} = homoindx(x[.,i+0]);
      if not(scalmiss(c));
	betaW[c,i+0] = (t[c]./x1[c,i+0])-((betaB[c,i+0].*x[c,i+0])./x1[c,i+0]);
      endif;
      if not(scalmiss(c0));
	betaW[c0,i+0] = t[c0].*ones(rows(c0),1);
      endif;
      if not(scalmiss(c1));
	betaW[c1,i+0] = miss(1,1).*ones(rows(c1),1);
      endif;
    endfor;
    
  endif;
  retp(betaW);
endp;


/* -----------------------------------------------------------------------
   y = rndisamp(&f,b,vc,dataset,sims);
**
** random numbers generated via importance sampling
** using the multivariate normal or t as the first approximation.
** 
** inputs: f() = likelihood function with arguments b(kx1),dataset
**         mu = kx1 means
**         vc = kxk variance matrix
**    dataset = input to f()
**       sims = number of simulations
**
** output:  y = nxk matrix of dependent Multivariate Normal Random Variables
**              each row of y is one 1xk simulation
**
** globals:  _Eprt if >=2 print progress reports
**           _Eisn = sims*_Eisn is the number of normals to draw before 
**                   resampling (default _Eisn=10)
**           _EisFac = factor to multiply normal variance matrix by
**                     or -1 to do normal approximation only
**                     or -2 to use maximum posterior estimates
**           _EisChk = 1 if save ln(imptce Ratio)~phi in lnir;
**                     0 otherwise save means
**           _Eist   = 0 (default) for normal random numbers; 
**                    >2 for multivariate t, with _Eist degrees of freedom
**           _EiLlikS = 1 save "lliksims" log-likelihood at each simulation 
**                      in dbuf; 0 save mean(lliksims) in dbuf
*/
proc rndisamp(&f,b,vc,dataset,sims);
  local f:proc;
  local lik,norm,i,max,psis,u,tst,r,lnir,ir,k,radd,keepsi,inf,ii,x,t,bb,meanir,
        resamp,vc0,keeplik;
  if rows(vc)/=rows(b) or cols(vc)/=rows(b);
    "rndisamp: input error";
    end;
  endif;
  
  if _EisFac==-1;                     @ normal approximation only @
    _Eres=vput(_Eres,miss(1,1),"lnir");
    _Eres=vput(_Eres,miss(1,1),"meanIR");
    _Eres=vput(_Eres,miss(1,1),"resamp");
    if _ei_vc[_Ghactual,1]/=-1;
      retp(rndmn(b,vc,sims));
    else;
      retp(rndtsn(b,vc,sims,_cml_bounds[1:rows(b),.],1e-3));
    endif;
  elseif _EisFac==-2;                 @ use maximum posterior estimates @
    retp(b'.*ones(_Esims,1));
  else;
    if _ei_vc[_Ghactual,1]/=-1;
      vc=vc/_EisFac;  @ "vc" is -Hessian @
    else;
      vc=vc*_EisFac;  @ "vc" is a variance matrix @
    endif;
  endif;
  
  k=sims*_Eisn;
  if _Eist==0;
    if _ei_vc[_Ghactual,1]/=-1;
      psis=rndmn(b,vc,k);
    else;                     @ use when _ei_vc={-1 0} @
      psis=rndtsn(b,vc,k,_cml_bounds[1:rows(b),.],1e-3);
      if ismiss(psis);
	if _Eprt>=2;
	  "ei: _EI_vc={-1 0} option failed. trying {-4 0} instead.";
	endif;
	retp(miss(1,1));
      endif;
    endif;
  else;
    vc=vc*(_Eist-2)/_Eist;
    psis=rndmt(b,vc,_Eist,k);
  endif;
  lik=zeros(k,1);
  norm=zeros(k,1);
  keepsi=0*b';
  keeplik=0;
  if _ei_vc[_Ghactual,1]/=-1;
    _Eivc=invpd(vc);
  else;
    _Eivc=vc;
  endif;
  
  for i (1, k, 1);
    if int(i/100)==i/100 and _Eprt>=2;
      i/10;
    endif;
    bb=(psis[i,.]'|_EisFix);
    lik[i]=sumc(packr(f(bb,dataset)));
    if _Eist==0;
      norm[i]=lnpdfmn2(psis[i,.]',b,0); @ var from _Eivc above @
    else;
      norm[i]=lnpdfmt(psis[i,.]',b,0,_Eist);
    endif;
  endfor;
  lnir=lik-norm;	@ ln(imptce ratio)  @

  if scalone(_EisChk);
    _Eres=vput(_Eres,lnir~psis,"lnir");
  else;
    max=maxc(lnir);
    meanIR=max+ln(meanc(exp(lnir-max)));    @ = ln(meanc(exp(lnir)) @
    _Eres=vput(_Eres,meanIR,"lnir");
  endif;

  if ismiss(lnir)==1;
    "Importance sampling failed! Using maximum posterior estimates";
    "(equivalent to _EisFac=-2). ";
    _Eres=vput(_Eres,-1,"lliksims");
    _Eres=vput(_Eres,-1,"resamp");
    retp(b'.*ones(_Esims,1));
  endif;
  
  resamp=0;
  r=0;
  do while r-1<sims;
    lnir=lnir-maxc(lnir);
    ir=exp(lnir);	        @ imptce ratio  @
    tst=(rndu(rows(ir),1).<=ir);
    keepsi=keepsi|selif(psis,tst);
    keeplik=keeplik|selif(lik,tst);
    r=rows(keepsi);
    lnir=delif(lnir,tst);
    psis=delif(psis,tst);
    lik=delif(lik,tst);
    if _Eprt>=2;
      resamp=resamp+1;
      "Got " r-1 ", resampling...";
    endif;
  endo;
  _Eres=vput(_Eres,resamp,"resamp");
  
  keepsi=keepsi[2:sims+1,.];
  keeplik=keeplik[2:sims+1,.];
  if _EiLlikS==1;
    _Eres=vput(_Eres,keeplik,"lliksims");
  else;
    _Eres=vput(_Eres,meanc(keeplik),"lliksims");
  endif;    
  retp(keepsi);
endp;  











